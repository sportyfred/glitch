<!DOCTYPE html>
<html>
<head>
<title>Distance2D - Processing.js on iPhone</title>
<meta name="viewport" content="width=320; initial-scale=1.0; maximum-scale=1.0; user-scalable=0;"/>
<meta name="apple-mobile-web-app-capable" content="yes" />
<meta name="apple-mobile-web-app-status-bar-style" content="black" />
<script src="iprocessing.js"></script>
<script src="init.js"></script>
</head>
<style type="text/css">
body {
	margin: 0;
}
</style>
<body>
<div>
<script type="application/processing">
// sample is from http://beautifulprogramming.com/infinite-arboretum/
/*

 Simple Image Glitching Processing
 datamoshing.com | 2016
 
 */

// image path is relative to sketch directory
PImage img;
String imgFileName = "MyImage";
String fileType = "jpg";

boolean glitchComplete = false;
boolean glitchSaved = false;

void setup() {
  img = loadImage(imgFileName+"."+fileType);

  // use only numbers (not variables) for the size() command, Processing 3
  size(1, 1);

  // allow resize and update surface to image dimensions
  surface.setResizable(true);
  surface.setSize(img.width, img.height);

  // load image onto surface
  image(img, 0, 0, img.width, img.height);
}


void draw() {
  if (!glitchComplete)
  {
    // load the image's pixels into .pixel array 
    img.loadPixels();

    boolean previousPixelGlitched = false;

    // random color 
    // 0-255, red, green, blue, alpha
    color randomColor = color(random(255), random(255), random(255), 255);

    // for each column of pixels
    for (int x = 0; x < img.width; x++) {

      // for each row of pixels
      for (int y = 0; y < img.height; y++) {

        // 25% chance to glitch this pixels, a second 80% chance if the previous pixel was glitched
        if (random(100) < 25 || (previousPixelGlitched == true && random(100) < 80))
        {
          previousPixelGlitched = true;

          // get the color for the pixel at coordinates x/y
          color pixelColor = img.pixels[y + x * img.height];

          // percentage to mix
          float mixPercentage = .5 + random(50)/100;

          // mix colors by random percentage of new random color
          img.pixels[y + x * img.height] =  lerpColor(pixelColor, randomColor, mixPercentage);
        } else
        {
          // didn't glitch this pixel
          previousPixelGlitched = false;

          // choose a new random mix color
          // 0-255, red, green, blue, alpha
          randomColor = color(random(255), random(255), random(255), 255);
        }
      }
    }

    img.updatePixels();
    glitchComplete = true;

    // load updated image onto surface
    image(img, 0, 0, img.width, img.height);

    // apply some filters
    // https://processing.org/reference/filter_.html
    
    // posterize filter
    // filter(POSTERIZE, 4);
    
    // dilate filter
    // filter(DILATE);
  }

  if (glitchComplete && !glitchSaved) {

    // save surface
    img.save(imgFileName+"_glitched.png");
    glitchSaved = true;
    println("Glitched image saved");
    println("Click or press any key to exit...");
  }
}

void keyPressed() {
  if (glitchSaved)
  {
    System.exit(0);
  }
}

void mouseClicked() {
  if (glitchSaved)
  {
    System.exit(0);
  }
}</script><canvas width="200" height="200"></canvas></div>
<div style="height:0px;width:0px;overflow:hidden;"></div>
<p>
based on <a href="http://ejohn.org/apps/processing.js/">Processing.js</a> by <a href="http://ejohn.org/">John Resig</a><br />
Example written by <a href="http://reas.com/">Casey Reas</a> and <a href="http://benfry.com/">Ben Fry</a>
</p>
</body>